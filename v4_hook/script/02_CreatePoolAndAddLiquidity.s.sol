// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {PoolKey} from "@uniswap/v4-core/src/types/PoolKey.sol";
import {CurrencyLibrary, Currency} from "@uniswap/v4-core/src/types/Currency.sol";
import {LiquidityAmounts} from "@uniswap/v4-core/test/utils/LiquidityAmounts.sol";
import {TickMath} from "@uniswap/v4-core/src/libraries/TickMath.sol";
import {LPFeeLibrary} from "v4-core/src/libraries/LPFeeLibrary.sol";
import {BaseScript} from "./base/BaseScript.sol";
import {LiquidityHelpers} from "./base/LiquidityHelpers.sol";
import {console2} from "forge-std/console2.sol";
import {IERC20} from "forge-std/interfaces/IERC20.sol";


// forge script script/02_CreatePoolAndAddLiquidity.s.sol --rpc-url arbitrum_sepolia --private-key 0xYOUR_PRIVATE_KEY --broadcast -vvvv --via-ir

// Anvil
// 1. set -a; source .env.anvil; set +a
// 2. forge script script/02_CreatePoolAndAddLiquidity.s.sol --rpc-url http://127.0.0.1:8545 --private-key 0xYOUR_PRIVATE_KEY --broadcast -vvvv --via-ir

contract CreatePoolAndAddLiquidityScript is BaseScript, LiquidityHelpers {
    using CurrencyLibrary for Currency;

    /////////////////////////////////////
    // --- Configure These ---
    /////////////////////////////////////

    //uint24 lpFee = 3000; // 0.30% 
    // Dynamic-fee pool: use ONLY the flag in PoolKey.fee
    uint24 lpFee = LPFeeLibrary.DYNAMIC_FEE_FLAG; // 0x800000
    int24 tickSpacing = 60;
    //uint160 startingPrice = 2 ** 96; // Starting price, sqrtPriceX96; floor(sqrt(1) * 2^96)

    // Let's set the initial price in human readable 
    function _sqrt(uint256 x) pure internal returns (uint256 y) {
        if (x == 0) return 0;
        uint256 z = (x + 1) / 2;
        y = x;
        while (z < y) { y = z; z = (x / z + z) / 2; }
    }

    // price = token1 per token0 = (num / den) in human units
    function sqrtPriceX96FromPriceFraction(
        uint256 num, uint256 den,
        uint8 decimalsToken0, uint8 decimalsToken1
    ) pure internal returns (uint160) {
        // ratio (raw units): amount1 / amount0
        // = (price * 10^dec1) / 10^dec0
        // = (num * 10^dec1) / (den * 10^dec0)
        uint256 numerator   = num * (10 ** decimalsToken1);
        uint256 denominator = den * (10 ** decimalsToken0);

        // ratioX192 = ratio * 2^192  (so sqrt gives X96)
        uint256 ratioX192 = (numerator << 192) / denominator;
        uint256 sqrtX96 = _sqrt(ratioX192);
        require(sqrtX96 <= type(uint160).max, "sqrtPriceX96 overflow");
        return uint160(sqrtX96);
    }

    // --- pick the target NAV ---
    uint256 ybtcPerUsdcNum = 1;  // 1 yBTC
    uint256 ybtcPerUsdcDen = 1;  // per 100,000 USDC

    // compute sqrtPriceX96
    uint160 startingPrice = sqrtPriceX96FromPriceFraction(
        ybtcPerUsdcNum,
        ybtcPerUsdcDen,
        6,   // USDC decimals (token0)
        6   // USDC decimals (token0)
        //18   // yBTC decimals (token1)
    );


    // --- liquidity position configuration --- //
    uint256 public token0Amount = 10e6;
    uint256 public token1Amount = 10e6;

    // range of the position, must be a multiple of tickSpacing
    int24 tickLower;
    int24 tickUpper;
    /////////////////////////////////////

    function run() external {
        PoolKey memory poolKey = PoolKey({
            currency0: currency0,
            currency1: currency1,
            fee: lpFee,
            tickSpacing: tickSpacing,
            hooks: hookContract
        });

        bytes memory hookData = new bytes(0);

        int24 currentTick = TickMath.getTickAtSqrtPrice(startingPrice);

        tickLower = truncateTickSpacing((currentTick - 750 * tickSpacing), tickSpacing);
        tickUpper = truncateTickSpacing((currentTick + 750 * tickSpacing), tickSpacing);

        
        // Converts token amounts to liquidity units
        uint128 liquidity = LiquidityAmounts.getLiquidityForAmounts(
            startingPrice,
            TickMath.getSqrtPriceAtTick(tickLower),
            TickMath.getSqrtPriceAtTick(tickUpper),
            token0Amount,
            token1Amount
        );
        console2.log("=== Pre-Transaction Balances ===");
        console2.log("Deployer:", deployerAddress);
        console2.log("Token0 balance:", currency0.isAddressZero() ? deployerAddress.balance : IERC20(Currency.unwrap(currency0)).balanceOf(deployerAddress));
        console2.log("Token1 balance:", IERC20(Currency.unwrap(currency1)).balanceOf(deployerAddress));
   
        // slippage limits
        uint256 amount0Max = token0Amount + 1;
        uint256 amount1Max = token1Amount + 1;

        (bytes memory actions, bytes[] memory mintParams) = _mintLiquidityParams(
            poolKey, tickLower, tickUpper, liquidity, amount0Max, amount1Max, deployerAddress, hookData
        );

        // multicall parameters
        bytes[] memory params = new bytes[](2);

        // Initialize Pool
        params[0] = abi.encodeWithSelector(positionManager.initializePool.selector, poolKey, startingPrice, hookData);

        // Mint Liquidity
        params[1] = abi.encodeWithSelector(
            positionManager.modifyLiquidities.selector, abi.encode(actions, mintParams), block.timestamp + 3600
        );

        // If the pool is an ETH pair, native tokens are to be transferred
        uint256 valueToPass = currency0.isAddressZero() ? amount0Max : 0;

        vm.startBroadcast();
        tokenApprovals();

        // Multicall to atomically create pool & add liquidity
        positionManager.multicall{value: valueToPass}(params);
        vm.stopBroadcast();
    }
}
